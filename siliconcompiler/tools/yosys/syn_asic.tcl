

########################################################
# Read Macro Libraries
########################################################
set sc_process     [dict get $sc_cfg pdk process]
set sc_mainlib     [lindex [dict get $sc_cfg asic targetlib] 0]
set sc_targetlibs  [dict get $sc_cfg asic targetlib]
set sc_tie         [dict get $sc_cfg library $sc_mainlib cells tie]
set sc_buf         [dict get $sc_cfg library $sc_mainlib cells buf]

set sc_tool  yosys
set sc_step  [dict get $sc_cfg arg step]
set sc_index [dict get $sc_cfg arg index]

#TODO: fix to handle multiple libraries
# (note that ABC and dfflibmap can only accept one library from Yosys, so
# for now everything needs to be concatenated into one library regardless)
set library_file [dict get $sc_cfg library $sc_mainlib nldm typical lib]

yosys read_liberty -lib $library_file

set sc_macrolibs [dict get $sc_cfg asic macrolib]

# Read macro library files, and gather argument list to pass into stat later
# on (for area estimation).
set stat_libs ""
foreach libname $sc_macrolibs {
    # TODO: we assume corner name is "typical" - this should probably be
    # documented somewhere?
    if {[dict exists $sc_cfg library $libname nldm]} {
        set macro_lib [dict get $sc_cfg library $libname nldm typical lib]
        yosys read_liberty -lib $macro_lib
        append stat_libs "-liberty $macro_lib "
    }
}

########################################################
# Synthesis
########################################################

# Although the `synth` command also runs `hierarchy`, we run it here without the
# `-check` flag first in order to resolve parameters before looking for missing
# modules. This works around the fact that Surelog doesn't pickle modules that
# are instantiated inside generate blocks that will get eliminated. This seems
# to give us the same behavior as passing the `-defer` flag to read_verilog, but
# `-defer` gave us different post-synth results on one of our test cases (while
# this appears to result in no differences). Note this must be called after the
# read_liberty calls for it to not affect synthesis results.
yosys hierarchy -top $sc_design

yosys synth "-flatten" -top $sc_design

yosys opt -purge

########################################################
# Technology Mapping
########################################################

# Techmap latches for supported open PDKs
if {$sc_process == "freepdk45"} {
    yosys techmap -map "cells_latch_freepdk45.v"
} elseif {$sc_process == "skywater130"} {
    yosys techmap -map "cells_latch_sky130hd.v"
} elseif {$sc_process == "asap7"} {
    yosys techmap -map "cells_latch_asap7.v"
}

yosys dfflibmap -liberty $library_file

yosys opt

source "$sc_refdir/syn_strategies.tcl"

set script ""
if {[dict exists $sc_cfg eda $sc_tool $sc_step $sc_index variable strategy]} {
    set sc_strategy [dict get $sc_cfg eda $sc_tool $sc_step $sc_index variable strategy]
    if { [dict exists $syn_strategies $sc_strategy] } {
        set script [dict get $syn_strategies $sc_strategy]
    } else {
        puts "Warning: no such synthesis strategy $sc_strategy"
    }
}

# TODO: other abc flags passed in by OpenLANE we can adopt:
# -D: clock period
# -constr: in the case of OpenLANE, an autogenerated SDC that includes a
#   set_driving_cell and set_load call (but perhaps we should just pass along a
#   user-provided constraint)

if {$script != ""} {
    yosys abc -liberty $library_file -script $script
} else {
    yosys abc -liberty $library_file
}

yosys stat -liberty $library_file {*}$stat_libs

########################################################
# Cleanup
########################################################

yosys setundef -zero

if {[llength $sc_tie] == 2} {
    set sc_tiehi [split [lindex $sc_tie 0] /]
    set sc_tielo [split [lindex $sc_tie 1] /]

    yosys hilomap -hicell {*}$sc_tiehi -locell {*}$sc_tielo
}

if {[llength $sc_buf] == 1} {
    set sc_buf_split [split $sc_buf /]
    yosys insbuf -buf {*}$sc_buf_split
}

yosys splitnets

yosys clean
